plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'com.google.protobuf' version '0.8.8'
}

ext {
    grpcVersion = '1.22.1'
    kotlinVersion = '1.3.31'
}

group = "com.inepex.nyomage"
version = "0.2.0"

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.7.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from tasks.dokka
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact(dokkaJar)
        }
    }
    repositories {
        maven {
            credentials {
                username "$inepexInternalMavenUser"
                password "$inepexInternalMavenPassword"
            }
            url "http://maven.inepex.com/repository/internal/"
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
